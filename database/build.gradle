apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse-wtp'
apply plugin: 'spring-boot'
apply plugin: "com.moowork.gulp"

defaultTasks 'makeReleaseZip'

dependencies {

  compile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.1'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: Versions.'spring-boot'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: Versions.'spring-boot'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging', version: Versions.'spring-boot'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-zuul', version: Versions.'spring-zuul'

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: Versions.'spring-boot'
  compile group: 'org.springframework.security.oauth', name: 'spring-security-oauth2', version: Versions.'spring-security-oauth2'
  compile group: 'org.springframework.security', name: 'spring-security-jwt', version: '1.0.4.RELEASE'
  compile group: 'mysql', name: 'mysql-connector-java', version: Versions.'mysql'

  // Test Dependency
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: Versions.'spring-boot'

}

task copyConf(type: Copy) {
  from ('conf') {
    include '**/*.properties'
    include '**/*.xml'
  }
  into "$buildDir/package/"
}

task copyScript(type: Copy) {
  from ('script') {
    include '**/*.bat'
    include '**/*.sh'
  }
  into "$buildDir/package/"
}


task copyChangelog(type: Copy) {
  from ('changelog') {
    include '**/*.sql'
    include '**/*.xml'
  }
  into "$buildDir/package/changelog"
}

task copyWar(type: Copy) {
  from war.archivePath
  into "$buildDir/package/"
  rename '.*.war', 'database.war'
}

task createBuildInfoFile (dependsOn: processResources) << {
  File buildInfoFile = file('src/main/resources/buildInfo.properties')
  Date startDate = new Date()
  Properties props = new Properties()
  props.setProperty('build.version', project.version.toString()) // see gradle.properties
  props.setProperty('build.timestamp', startDate.format("yyyy-MM-dd'T'HH:mm:ssZ"))
  props.setProperty('build.name', "rc-lab")
  props.setProperty('build.description', project.description.toString()) // See above
  props.store(buildInfoFile.newWriter(), null)
}
task makeReleaseZip(type: Zip) {
  dependsOn 'build'
  from(project.buildDir.path + '/package') { include '**/*.*' }
}

build.dependsOn copyConf,copyScript,copyChangelog,copyWar
compileJava.dependsOn createBuildInfoFile